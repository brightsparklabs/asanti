/*
 * Maintained by brightSPARK Labs.
 * www.brightsparklabs.com
 */

// -----------------------------------------------------------------------------
// PLUGINS
// -----------------------------------------------------------------------------

plugins {
    // Apply the brightSPARK Labs plugin for standardisation
    id 'com.brightsparklabs.gradle.baseline' version "0.2.0"

    // Apply the java plugin to add support for Java
    id 'java'

    // Apply the application plugin to add support for building an application
    id 'application'

    // Apply the library plugin to add support for exposing as a library
    id 'java-library'

    // Apply the groovy plugin to also add support for Groovy (needed for Spock)
    id 'groovy'

    // Apply bintray plugin for publishing artifacts
    id "com.jfrog.bintray" version "1.8.5"
    id "maven-publish"
}

// -----------------------------------------------------------------------------
// IMPORTS
// -----------------------------------------------------------------------------

apply from: 'startScripts.gradle'

// -----------------------------------------------------------------------------
// REPOSITORIES/DEPENDENCIES
// -----------------------------------------------------------------------------

repositories {
    jcenter()
}

dependencies {
    def versions = [
        powermock: "2.0.9",
        slf4j: "1.7.30"
    ]

    implementation "com.google.guava:guava:29.0-jre"
    implementation "org.bouncycastle:bcprov-jdk15on:1.65"
    implementation "org.slf4j:slf4j-api:${versions.slf4j}"
    implementation "joda-time:joda-time:2.10.6"

    implementation "commons-cli:commons-cli:1.4"

    testImplementation 'junit:junit:4.13'

    testImplementation "org.powermock:powermock-api-mockito2:${versions.powermock}"
    testImplementation "org.powermock:powermock-module-junit4:${versions.powermock}"
    testImplementation 'org.hamcrest:hamcrest-all:1.3'

    testImplementation "org.codehaus.groovy:groovy-all:3.0.3"
    testImplementation "org.spockframework:spock-core:2.0-M2-groovy-3.0"

    testRuntimeOnly "cglib:cglib-nodep:3.3.0"

    runtimeOnly "org.slf4j:slf4j-log4j12:${versions.slf4j}"
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

// -----------------------------------------------------------------------------
// PROJECT IDENTIFICATION
// -----------------------------------------------------------------------------

project.description = 'A dynamic ASN.1 Parser'
project.ext.url = 'https://github.com/brightsparklabs/asanti'
project.ext.scm = 'scm:git@github.com:brightsparklabs/asanti.git'

// -----------------------------------------------------------------------------
// CONFIGURE: ErrorProne
// -----------------------------------------------------------------------------

project.tasks.named("compileJava").configure {
    options.errorprone.disable("MissingSummary")
    options.errorprone.disable("JavaLangClash")
}

// -----------------------------------------------------------------------------
// CREATE START SCRIPTS (via application)
// -----------------------------------------------------------------------------

mainClassName = 'com.brightsparklabs.asanti.AsantiCli'

// -----------------------------------------------------------------------------
// PUBLISH ARTIFACTS (via bintray)
// -----------------------------------------------------------------------------

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Publication']
    override = true
    publish = true
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'brightsparklabs'
        licenses = ['MIT']
        vcsUrl = project.ext.url
        version {
            name = project.version
            vcsTag = project.version
            released = new Date()
        }
    }
}

// Include javadoc and sources (requirements for publishing to Maven Central)
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

def pomConfig = {
    name project.name
    packaging 'jar'
    description project.description
    url project.url

    scm {
        connection project.scm
        developerConnection project.scm
        url project.scm
    }

    licenses {
        license {
            name 'MIT License'
            url 'http://www.opensource.org/licenses/mit-license.php'
        }
    }

    developers {
        developer {
            id 'brightsparklabs'
            name 'brightSPARK Labs'
            email 'enquire@brightsparklabs.com'
        }
    }
}

publishing {
    publications {
        Publication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.children().last() + pomConfig
            }
        }
    }
}
